-- Statement evaluator.

import State;
import Expr;
import World;

-- Evaluates a statement "stmt" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A statement is represented by a data structure of the following shape:
--
-- stmt = Assn    (string, expr)     |
--        Seq     (stmt, stmt)       |
--        Skip                       |
--        Read    (string)           |
--        Write   (expr)             |
--        If      (expr, stmt, stmt) |
--        While   (expr, stmt)       |
--        DoWhile (stmt, expr)

fun eval (c, stmt) {
  fun assnImpl ([s, w], name, expr) {
    [s <- [name, evalExpr (s, expr)], w]
  }

  fun readImpl ([s, w], name) {
    case readWorld (w) of 
      [value, newWorld] -> [s <- [name, value], newWorld]
    esac
  }

  fun writeImpl ([s, w], expr) {
    [s, writeWorld (evalExpr (s, expr), w)]
  }

  fun ifImpl (c@[s, w], condition, then_stmt, else_stmt) {
    case evalExpr (s, condition) of
      0 -> eval (c, else_stmt)
    | _ -> eval (c, then_stmt)
    esac
  }

  fun whileImpl (c@[s, w], condition, body_stmt) {
    case evalExpr (s, condition) of
      0 -> c
    | _ -> whileImpl (eval (c, body_stmt), condition, body_stmt)
    esac
  }

  fun doImpl (c@[s, w], body_stmt, condition) {
    whileImpl (eval (c, body_stmt), condition, body_stmt)
  }

  case stmt of
    Assn (name, expr)         -> assnImpl (c, name, expr)
  | Seq (lhs_stmt, rhs_stmt)  -> eval (eval (c, lhs_stmt), rhs_stmt)
  | Skip                      -> c
  | Read (name)               -> readImpl (c, name)
  | Write (expr)              -> writeImpl (c, expr)
  | If (cond, if_st, else_st) -> ifImpl (c, cond, if_st, else_st)
  | While (cond, body_st)     -> whileImpl (c, cond, body_st)
  | DoWhile (body_st, cond)   -> doImpl (c, body_st, cond)
  esac
}

-- Evaluates a program with a given input and returns an output
public fun evalStmt (input, stmt) {
  eval ([emptyState, createWorld (input)], stmt).snd.getOutput
}
